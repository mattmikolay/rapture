plugins {
    id 'application'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.0'
}

group 'com.mattmik'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClassName = 'com.mattmik.rapira.RaptureCommandKt'
}

def antlrOutputDirectory = "src/main/java/com/mattmik/rapira/antlr"
sourceSets.main.antlr.srcDirs = ["src/antlr"]
generateGrammarSource {
    arguments += ["-visitor", "-no-listener", "-package", "com.mattmik.rapira.antlr"]
    outputDirectory = file(antlrOutputDirectory)
}

ktlint {
    version = "0.39.0"
}

dependencies {
    def kotest_version = "4.2.6"
    antlr "org.antlr:antlr4:4.8"
    implementation "com.github.ajalt.clikt:clikt:3.0.1"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
    testImplementation "io.kotest:kotest-property:$kotest_version"
    testImplementation "io.mockk:mockk:1.10.0"
}

compileKotlin.dependsOn generateGrammarSource

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

clean.doFirst {
    delete "${rootDir}/${antlrOutputDirectory}"
}
