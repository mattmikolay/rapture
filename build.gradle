plugins {
    id 'application'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jlleitschuh.gradle.ktlint' version '9.3.0'
}

group 'com.mattmik'
version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClassName = 'com.mattmik.rapira.RaptureCommandKt'
}

def antlrOutputDirectory = "src/main/java/com/mattmik/rapira/antlr"
sourceSets.main.antlr.srcDirs = ["src/antlr"]
generateGrammarSource {
    arguments += ["-visitor", "-no-listener", "-package", "com.mattmik.rapira.antlr"]
    outputDirectory = file(antlrOutputDirectory)
}

ktlint {
    version = "0.37.2"
}

dependencies {
    def kotest_version = "4.2.0"
    antlr "org.antlr:antlr4:4.8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.ajalt:clikt:2.8.0"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-property-jvm:$kotest_version"
    testImplementation "io.mockk:mockk:1.9.3"
}

compileKotlin.dependsOn generateGrammarSource

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

clean.doFirst {
    delete "${rootDir}/${antlrOutputDirectory}"
}
